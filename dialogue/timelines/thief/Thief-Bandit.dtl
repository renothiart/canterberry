if {Thief.MetBandit} == false:
	Bandit: My sweet princess! I’ve been waiting for you.
	Narrator: The Bandit and the Thief embrace. It’s been a long time since she last saw her baddie boyfriend.
	Bandit: Remember the heist we discussed? I think now would be the perfect time to pull it off. You remember that secret passageway in the castle, right?
	Narrator: The Thief nods. She had been the one to tell the Bandit about it in the first place, after all.
	Bandit: Great! And you can distribute it fairly amongst the townspeople when you're done. I believe in you, princess.
	Narrator: The Bandit winks and blows a kiss.
	set {Thief.MetBandit} = true
else:
	if {Thief.CollectTreasure} == false:
		Bandit: My darling princess! It’s always lovely to see you. Let me know when you’ve got the [b]treasure[/b] from the [b]Castle[/b].
		[end_timeline]
	else:
		if {Thief.DonateCount} <= 3:
			Bandit: My precious princess, you’ve returned! And you got the treasure?
			Narrator: The Thief splits the treasure with the Bandit.
			set {Thief.DonateBandit} = true
			set {Thief.DonateCount} += 1
			Bandit: I appreciate this, I’ll be sure to share it with the fruit across the sea on my next journey.
			Bandit: But I’m not leaving just yet, I’d love to spend more time with you while I can.
			Narrator: The Bandit and the Thief share a sweet kiss.
			[end_timeline]
	if {Thief.DonateCount} == 4:
		Bandit: My rosy princess, you gave the treasure to all the townspeople? You're amazing, I love you.
		Narrator: And so the Thief completed her quest of giving back to the fruit of the kingdom...her way, of course.
		set {Thief.CompletedThiefQuest} = true
